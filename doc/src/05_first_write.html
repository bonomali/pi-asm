Using the sys_write syscall to output text

<p>
And so we arrive, at last, at "Hello, World". This example demonstrates
how to use <code>sys_write</code> to write to the console, and 
introduces some other new assembly-language features. 
I will show the example in its entirety, but some of the code is
the same as in the previous example.  
</p>

<h3>Example</h3>

<p>
Here is the code. It just outputs "Hello, World" to the console.
</p>

<pre class="codeblock" lang="asm">
// Outputs a simple message using sys_write

.text

SYS_EXIT = 1
SYS_WRITE = 4
STDOUT = 1

.global _start

// Exit the program.
//   On entry, r0 should hold the exit code
exit:
    mov    %r7, $SYS_EXIT
    swi    $0

_start:
    // Use the sys_write syscall to output a string
    mov    %r7, $SYS_WRITE
    mov    %r0, $STDOUT
    ldr    %r1, =msg // Store the address of the message in r1
    mov    %r2, $13  // Store the length of the message in r2
    swi    $0

    // Now exit
    mov    %r0, $0
    b      exit
msg:
    .ascii "Hello, World\n"
</pre>

<h3>Defining data</h3>

<p>
The text message "Hello, World" is a piece of data larger than a single
number. We've already seen how an integer number can be loaded 
directly into a register using an immediate instruction like
<code>mov %r0, $32</code>. However, we can't load a whole string of
text into a 32-bit register. We can, and will, load the <i>address</i>
of the string into a register, but to do that we have to define the
string, and know its address.
</p>
<p>
The assembler provide a straightforward way to introduce data of various
types into the object file. My example uses this method for a text string:
</p>

<pre class="codeblock" lang="asm">
msg:
    .ascii "Hello, World\n"
</pre>

<p>
As in most other programming languages, <code>\n</code> is a code that 
means 'new line'. Although it is written as two symbols in the source 
-- '\' and 'n' -- it only occupies one byte in memory.
</p>
<p>
<code>msg</code> is just a label. When the program is assembled, references
to the label <code>msg</code> will be replace with its address.
The assembler supports many other data types -- 
<code>.byte</code>, <code>.word</code>, etc. 
</p>

<h3>The sys_write syscall</h3>

<p>
The <code>sys_write</code> syscall (number 4 in ARM Linux) is a little
more complicated than <code>sys_exit</code>. It takes three arguments:
</p>
<ul>
<li><p><code>r0</code> -- the file descriptor. This is an integer
that identifies the file or device to write to. "standard out" 
will always be file 1 on Linux terminals or consoles. Standard error
is file 2.</p></li>
<li><p><code>r1</code> -- the address in memory of the data to write.</p></li>
<li><p><code>r2</code> -- the number of bytes to write.</p></li>
</ul>
<p>
As with all ARM Linux syscalls, the syscall number (4) goes into
<code>r7</code>.
</p>

<h3>The ldr instruction</h3>
<p>
<code>ldr</code> is <b>l</b>oa<b>d</b> <b>r</b>egister. In this
example, <code>ldr</code> is used in a way that  
is conceptually exactly the same as the immediate mode of <code>mov</code>, 
-- to transfer a number into a register. This instruction: 
</p>

<pre class="codeblock" lang="asm">
    ldr    %r1, =msg 
</pre>

<p>
transfers to register <code>r1</code> the numerical address labeled by <code>msg:</code>.
</p>

<p>
<code>ldr</code> also has an <i>indirect</i> mode, like this:
</p>

<pre class="codeblock" lang="asm">
    ldr    %r1, [r4] 
</pre>

<p>
In this mode, the value of the register <code>r4</code> is treated
as an address in memory, and <code>r0</code> is loaded with the 
data in memory at that address. It is the square brackets that
indicate the indirect mode of operation.
</p>
<p>
We don't need to use the indirect form of <code>ldr</code> in this
example, but will need it later.
</p>

<h3>ldr is not what it seems</h3>
<p>
If the use of <code>ldr</code> in this example is conceptually the
same as <code>mov</code>, then why not just use <code>mov</code> as
we did previously? Answering this question requires delving into
the internal operation of the assembler, but it's necessary to do 
this, in order
to write efficient code.
</p>
<p>
So why could we not, in the present example, instead of <code>ldr</code>
use this?:
</p>
<pre class="codeblock" lang="asm">
    mov    %r1, =msg 
</pre>

<p>
After all, I've already said that the immediate modes of <code>mov</code>
and <code>ldr</code> are conceptually equivalent. The 
reason for not using <code>mov</code> is that the immediate
operand to <code>mov</code> is of limited size. I already touched on
this back in example 1, and hinted at it again in example 3. The 
immediate operand to <code>mov</code> can only be 11 bits long, but the
register can store a 32-bit number. This limitation arises from the 
way that the operand is encoded, using only 11 bits in the instruction.
It isn't the case that we can encode <i>any</i> 11-bit number -- numbers
that are powers of two are encoded differently. The assembler will stop
with an error if you try to use an immediate number that can't be
encoded using the CPU's rules.  
</p>
<p>
In practice, the address of the message labeled <code>msg:</code>
<i>might</i> fit into a <code>mov</code> -- it's just about
possible, because the program is so small. However, it's unwise to
rely on this in a real program.
</p>
<p>
On the other hand, the <code>ldr</code> operation can encode any
32-bit number at all. If you're wondering how we can encode a
32-bit number into an instruction which is only 32 bits in total
the answer is, of course: we can't. It's impossible.
</p>
<p>
The fact is that <b><code>ldr</code>'s immediate mode is an illusion</b>.
<code>ldr</code> has no immediate mode -- only an indirect mode, 
where data is loaded from an address in memory. An instruction like this:
</p>
<pre class="codeblock" lang="asm">
    ldr    %r1, =42
</pre>

<p>
is actually a pseudo-instruction. The assembler converts this
instruction into something like:
</p>
<pre class="codeblock" lang="asm">
    ldr    %r1, [foo]
foo: 
    .word 42
</pre>

<p>
That is, the assembler simulates an immediate operand by storing the
operand's value in memory, and generating an indirect access to the
stored value. That's how <code>ldr</code> can store a 32-bit value
using a 32-bit instruction code. 
</p>
<p>
The downside, and the reason we prefer to use <code>mov</code> if
we can, is that executing a pseudo-immediate <code>ldr</code> will
take much longer than the truly immediate <code>mov</code>. As well
as the CPU having to read and decode the instruction code itself 
from memory,
which is all that <code>mov</code> requires, using <code>ldr</code>
requires some additional arithmetic and then a further read from
memory. Using <code>mov</code>, where possible, is faster, as
well as using less storage. To be fair, we won't notice the 
difference of less than a microsecond in a trivial program like this, but
those microseconds add up when there are millions of them. 
</p>
<p>
In short: <code>mov</code> is an immediate instruction -- all
the data it needs is in the instruction itself. <code>ldr</code>
is an <i>indirect</i> instruction that reads from memory, but the
assembler simulates an immediate mode for <code>ldr</code> because
<code>mov</code> has a range limitation.
</p>


<h3>Where is the data?</h3>
<p>
You may have noticed that the data that forms the "Hello, World" message
is just tacked on the end of the program code. This is a reasonable
thing to do, but a little unconventional -- usually the program's
constant data will be placed in a separate memory segment. I'll illustrate this
in the next example. 
</p>
<p>
One disadvantage of using the <code>.text</code> (program code) section
for data is that if you try to disassemble the code, or use an
interactive debugger, the tools won't be able to tell the difference between
genuine program code and your data. This won't do any harm, but it will
make the tools confusing to use.
</p>

<h3>Summary</h3>
<ul>
<li><p>The <code>sys_write</code> syscall outputs data to a file or device.</p></li>
<li><p>The <code>ldr</code> instruction reads data from memory into a register.</p></li>
<li><p><code>ldr</code> can be used to overcome the range limitation in
the <code>mov</code> instruction, but <code>mov</code> -- where it 
can be used -- is faster and uses
less storage.</p></li>
</ul>




